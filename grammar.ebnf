program :: (stmt NEWLINE)+ EOF

block :: INDENT (stmt NEWLINE)+ DEDENT

stmt :: 'let' let_prefix '=' compound
      | 'export' NAME '=' compound
      | 'export' NAME 'as' 'foreign' (NAME | STRING)
      | 'import' (NAME | STRING) ('as' NAME)?
      | 'macro' NAME fnparams 'as' fnparams block
      | macro_exp
      | 'link' STRING
      | 'library' STRING
      | if_stmt
      | 'catch' NAME block
      | 'for' let_prefix 'in' binexpr block
      | 'with' binexpr ('as' NAME (',' NAME)*)
      | 'while' binexpr block
      | 'until' binexpr block
      | 'loop' block
      | 'pass'
      | 'break' ('if' binexpr)?
      | 'continue' ('if' binexpr)?
      | 'return' compound?
      | 'save' compound
      | assn_prefix ('=' compound | fnargs | ':' NAME  fnargs)

if_stmt :: 'if' binexpr block (NEWLINE 'else' (if_stmt | block))?

macro_exp :: '@' NAME ('.' NAME)* ***

let_prefix :: '[' let_prefix (',' let_prefix)* ']'
            | NAME

assn_prefix :: '[' assn_prefix (',' assn_prefix)* ']'
             | prefix ('.' NAME | '[' binexpr ']')*

array_expr :: '[' (binexpr (',' binexpr)*)? ','? ']'

dict_item :: ((NAME | '[' binexpr ']') '=' binexpr)

dict_expr :: '{' (dict_item (',' dict_item)*)? ','? '}'

fnargs :: '(' (binexpr (',' binexpr)*)? ')'

fnargblock :: INDENT (compound NEWLINE)+ DEDENT

fnparams :: '(' (NAME (',' NAME)*)? ')'

compound :: macro_exp
          | 'func' (NAME | STRING)? fnparams ('->' binexpr | block)
          | binexpr

binexpr :: unexpr (OPERATOR unexpr)*

unexpr :: ('-' | '!') simple
        | simple

simple :: 'func' fnparams '->' binexpr
        | 'foreign' (NAME | STRING) fnparams
        | array_expr
        | dict_expr
        | primary

primary :: prefix ('?'? fnargs | ':' NAME '?'? fnargs | '.' NAME | '[' binexpr ']')*

prefix :: '(' binexpr ')'
        | NAME | INT | FLOAT | BOOL | STRING | NULL | TABLE
